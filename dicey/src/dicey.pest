WHITESPACE = _{ WHITE_SPACE }

declaration = { "let" ~ identifier ~ "=" ~ expression ~ ";" }
expression = { ( if_statement | block | logic_or ) }
block = { "{" ~ declaration* ~ expression ~ "}" }
if_statement = { "if" ~ expression ~ "then" ~ expression ~ "else" ~ expression }

logic_or = { logic_and ~ ( "or" ~ logic_and )* }
logic_and = { equality ~ ( "and" ~ equality )* }

equality = { comparison ~ ( equality_op ~ comparison )* }
equality_op = { ( "==" | "!=" ) }

comparison = { term ~ ( comparison_op ~ term )* }
comparison_op = { ( ">=" | ">" | "<=" | "<" ) }

term = { factor ~ ( term_op ~ factor )* }
term_op = { ( "-" | "+" ) }

factor = { unary ~ ( factor_op ~ unary )* }
factor_op = { ( "/" | "*" | "%" ) }

unary = { unary_op* ~ call }
unary_op = { ( "!" | "-" ) }

call = { ( primary ~ ( "(" ~ call_args ~ ")" )* ) }
call_args = { ( expression ~ ("," ~ expression)* ~ ","? )? }

primary = _{ ( number_or_roll | boolean | function | identifier | "(" ~ expression ~ ")" ) }
number_or_roll = @{
    (
          "d" ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*
        | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* ~ ( ("d" ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | ("." ~ ASCII_DIGIT*)? )
        | "0" ~ ("." ~ ASCII_DIGIT*)?
        | ("." ~ ASCII_DIGIT+)
    )
}
boolean = @{ "true" | "false" }
identifier = @{ ( XID_START | "_" ) ~ XID_CONTINUE* }
function = { parameters ~ expression }
parameters = { "|" ~ ( identifier ~ ("," ~ identifier)* ~ ","? )? ~ "|" }

file = { SOI ~ declaration* ~ expression ~ EOI }